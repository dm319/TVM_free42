;-----------------------------------------------------------------------------
; #  | N  | I%YR | PV | PMT   | FV | P/YR | End ||   TVM v9    |   Plus42    |
;----|----|------|----|-------|----|------|-----||-------------|-------------|
; 01 | ?  |  5%  | 1M | -5k   | 0  |  12  | yes ||    430.9175 |    430.9175 |
; 02 | ?  |  5%  | 1M | -2000 | 0  |  12  | no  ||    425.9790 |    425.9790 |
; 03 | 1k |  ?   | 1M | -2000 | 0  |  12  | yes ||      1.9113 |      1.9113 |
; 04 | 1k |  ?   | 1M | -2000 | 0  |  12  | no  ||      1.9164 |      1.9164 |
; 05 | 1k |  5%  | ?  | -2000 | 0  |  12  | yes || 472493.4501 | 472493.4501 |
; 06 | 1k |  5%  | ?  | -2000 | 0  |  12  | no  || 474462.1728 | 472493.4501 |
; 07 | 1k |  5%  | 1M | ?     | 0  |  12  | yes ||  -4232.8629 |  -4232.8629 |
; 08 | 1k |  5%  | 1M | ?     | 0  |  12  | no  ||  -4215.2992 |  -4215.2992 |
;-----------------------------------------------------------------------------

; Original test cases:
;------------------------------------------------------------------------------------------------------
; #  | N            | I%YR      | PV       | PMT          | FV        | P/YR | End || TVM v9 | Plus42 |
;----|--------------|-----------|----------|--------------|-----------|------|-----||--------|--------|
; 1  | 38 x 12      | 5.25%     | 270'000  | ?            | 0         | 12   | yes ||  34    |  34    |
; 1b | 38 x 12      | ?         | 270'000  | -14'584/12   | 0         | 12   | yes ||  33.1  |  33.1  |
; 2  | 360          | 15% → 12% | 100'000  | ?-?          | 0         | 12   | yes ||  33.2  |  32.4  |
; 3  | 60x60x24x365 | 10%       | 0        | -0.01        | ?         | =N   | yes ||  32.7  |  33.7  |
; 4  | 480          | 0 → ?     | 100'000  | ?→ PMT       | 0         | 12   | yes ||  Inf   |  Inf   |
; 5  | 10           | ?         | 50       | -30          | 400       | 1    | yes ||  32.3  |  31.8  |
; 6  | 10           | ?         | 50       | -30          | 80        | 1    | yes ||  32.5  |  32.3  |
; 7  | 10           | ?         | -100     | 10           | 1e-10     | 12   | yes ||  31.1  |  31.4  |
; 8  | 32           | ?         | -999'999 | 0            | 1e6       | 1    | yes ||  39.2  |  35.5  |
; 9  | ?            | 25        | 100000   | -2083.333334 | 0         | 12   | yes ||  33.4  |  29.8  |
; 10 | ?            | 25        | 100000   | -2040.816327 | 0         | 12   | no  ||  33.4  |  30.3  |
; 11 | 60x24x365    | 1/6% → ?  | 0        | -0.01        | ?→ FV     | =N   | yes ||  28.6  |  31    |
; 12 | 40           | ? → I%YR  | 900      | -400         | -1000 → ? | 1    | no  ||  Inf   |  23.7  |
;------------------------------------------------------------------------------------------------------

; FLAG 00  0 → Nothing   is returned (default)
;          1 → Something is returned. Used by N, I%YR, PV, PMT, FV, →AER
; FLAG 01  Used for temporary purposes 
; "p"      0 → End_Mode,   use TVM menu (which shows "START" & "END•")
;          1 → Start_Mode, use MENU2    (which shows "STAR•" & "END")

; On a first run, Plus42 doesn't need FV entered to do #1, Duncan's program does. I changed that. 

; Plus42's menus stay at their level and only change via the up/down buttons. Different for :TVM
; Plus42 doesn't AVIEW anything when you do CLEAR, this program does.
; Plus42 uses BEGIN, :TVM uses START  ???
; Could we make a SHOW TVM so you can see ALL the 5 primary numbers? (only practical in Plus42)
; Made LOCAL variables for: den, num, k, y, f0, f, g, pv, fv, fpp, fp2, eps, a, b, c, d
; In Plus42, if you change modes then you can't recalculate a variable with one press. You can here :-) 

; Darn it - this will cause trouble, it also requires Free42 3.0...
; NOTE: In Free42 (but not in Plus42), Allow Big Stack (NSTK) mode must be turned on
;       in the Preferences in order to use the NSTK or LNSTK functions. If not, you
;       will get the Big Stack Disabled error message when trying to use them.

; Add in LOCK as a program step
; Many expensive DROP commands done vs judiciously using CLX
; Changed local labels for everything except the untouched I%YR stuff to uppercase A-F (local Alpha Labels)
; Changed my CONSTANTS program to use local Alpha Labels a-e
; "I%YR" is the only function that still uses LNSTK
; Fixed: PMT had a bug in the formula for START mode when i≠0
; There is no LBL "IYR?"  ???? What does it do?
; What does "→AER" do?
; Removed STO "RESET", "CLEAR", "START", "STAR•", "END" & "END•" because they are pointless,
; also removed (the original) FLAG 01 and merged everything into "p"
; Can I rename "p" to something else (unless it's already a standard variable name)
; This program clutters Free42 with global variables that may exist in other user programs :-( 
; Removed STO IND "var" & DROP before RTN in all MVAR routines that return a value
; Change Result VIEW to var:result vs var=result to distinguish it from an MVAR STO. Plus42 is super confusing. 
; Should CLEAR also reset the START/END mode?

; ERROR HANDLING:
; Need to be robust against a consecutive press when all the necessary variables haven't been entered.
; Need to be robust against impossible solutions yielding an error.
; Should I fix the fact that this mangles LASTx?
; If the calculation for "I%YR" breaks you'll unexpectedly end up in NSTK mode :-( 
; Need to do more error checking e.g. for illegal non-real values 
; If you do a first run and stupidly just press "N" then you get "Nonexistent", however program 
; execution has stopped at the error so now even doing START or STOP is broken because the program pointer 
; is still in the same place that caused the error. Need a way to catch these silly errors.
; Plus42 gives you N=1 in this case. It also filters out N=0 as Invalid Data
; Filter for N not being an integer and <0, PV=FV (?), PMT=0 (?), existence of required variables per function,
; odd values in START and END (strictly speaking they shouldn't exist)

001▸LBL ":TVM" ; primary menu (START, END•) to indicate END mode
002 MVAR "N"
003 MVAR "I%YR"
004 MVAR "PV"
005 MVAR "PMT"
006 MVAR "FV"
007 MVAR "P/YR"
008 MVAR "START"
009 MVAR "END•"
010 MVAR "IYR?"
011 MVAR "→AER"
012 MVAR "RESET"
013 MVAR "CLEAR"
    XEQ "RESET"    ; Main initialization. Does NOT clear all variables.
014 GTO  A

015▸LBL "MENU2" ; second menu (STAR•, END) to indicate begin mode
016 MVAR "N"
017 MVAR "I%YR"
018 MVAR "PV"
019 MVAR "PMT"
020 MVAR "FV"
021 MVAR "P/YR"
022 MVAR "STAR•"
023 MVAR "END"
024 MVAR "IYR?"
025 MVAR "→AER"
026 MVAR "RESET"
027 MVAR "CLEAR"

;---;-----------------------------
030▸LBL A          ; MAIN RUN LOOP
;---;-----------------------------
    CF 00          ; Default Flag 00 state (MVARs routines return nothing)
    FC? 80         ; Test for NSTK
    GTO H          ; If 4STK then all is good - just bring up VARMENU
    DEPTH          ; If empty NSTK then put 0.00 in X because a MENUVAR store 
    X≠0?           ;   operation when X="" makes an unrecoverable error
    DROP
    LBL H          ; At this point there is something guaranteed to be in X
    X<> "P/YR"     ; Temporarily save X and load "P/YR"
032 "P/YR="        ; Update the Alpha-register with the TVM status
034 X<> "P/YR"     ; Restore X & "P/YR"
035 AIP            ; append to the alpha register 
036 0=? "p"
037 ├", END mode"
038 0≠? "p"
039 ├", START mode"
    LBL G          ; Put 0.00 in X if in NSTK with a blank X-register 
043 AVIEW          ; Show the TVM state if a menu variable wasn't solved.
044 CLA
045 VARMENU ":TVM"
046 0≠? "p"        ; select correct menu according to START or STOP mode
047 VARMENU "MENU2"
048 STOP
;                  ; When a VARMENU key is pressed immediately following another, it 
;                  ; puts MVAR in the Alpha-register and does XEQ IND MVAR from here.
;                  ; Otherwise it performs STO IND MVAR and a VIEW IND MVAR and doesn't 
;                  ; advance the program pointer. This allows dumb things   
;                  ; like storing values in START or END without setting the mode :-( 
059 ASTO "var"
060 XEQ IND "var"  ; XEQ the menu variable. X contains the result if a calculation was done.
    FC?C 00        ; Was something was placed in X?
    GTO A          ; No  - update the Alpha-register with the TVM status
063 STO IND "var"  ; Yes - update the Alpha-register with the TVM result
064 ├":"           ; Use ":" vs "=" to distinguish internal MVAR store vs MVAR result
065 ARCL ST X
067 GTO G

;---;--------------------------------------------------------------------------------------------
068▸LBL "RESET" ; TVM INITIALIZATION,  p=0 (END mode), FV=IYR?=RESET=0, P/YR=12
;   ; If the X-register was BLANK it will return with 0, otherwise the stack is untouched
;   ; ??? I also changed this to do FV=0 (consistent with Plus42 I think)
;---;--------------------------------------------------------------------------------------------
    FUNC 00
071 12
072 STO "P/YR"   ; Set Payments/Yr to 12
073 0
076 STO "p"      ; Set p=0 for END mode
074 STO "FV"     ; Set FV=0
075 STO "IYR?"   ; Set IYR?=0
077 RTN
;---;--------------------------------------------------------------------------------------------
078▸LBL "CLEAR"  ; clear all variables and SF 00. Leaves the stack untouched
;---;--------------------------------------------------------------------------------------------
    FUNC 00
080 0
081 STO "PV"
082 STO "PMT"
084 STO "I%YR"
086 STO "N"
083 STO "FV"
085 STO "IYR?"
087 RTN
;---;--------------------------------------------------------------------------------------------
088▸LBL "START"
089▸LBL "STAR•"  ; Start mode ("p"=1)
;---;--------------------------------------------------------------------------------------------
    FUNC 00
080 1
090 STO "p"      ; Set p=1 for START mode
094 RTN
;---;--------------------------------------------------------------------------------------------
095▸LBL "END" 
096▸LBL "END•"   ; End mode ("p"=0)
;---;--------------------------------------------------------------------------------------------
    FUNC 00
    0
099 STO "p"      ; Set p=0 for END mode
;   ; CLV "START"  ; In case those was erroneously set by the user
;   ; CLV "STAR•"  ; In case those was erroneously set by the user
;   ; CLV "END"    ; In case those was erroneously set by the user
;   ; CLV "END•"   ; In case those was erroneously set by the user
100 RTN
;---;--------------------------------------------------------------------------------------------
101▸LBL "P/YR" ; ??? Why is this needed?
;---;--------------------------------------------------------------------------------------------
;   ; This should be OK given that the main run loop ensures X≠"" in NSTK mode
102 STO "P/YR"
103 RTN
;---;--------------------------------------------------------------------------------------------
104▸LBL "N"    ; Solve for N (for i=0 & i≠0) (+2)
;---;--------------------------------------------------------------------------------------------
105 FUNC 01
106 SF 00      ; To signify this routine returns an answer in X
107 CF 01      ; Do NOT calculate "F" (which requires "N") within XEQ I
108 XEQ I      ; creates "i" and leaves it in X 
109 X=0?
110 GTO B      ; <---- Calculate N(i=0)  -(PV+FV)/PMT
111 1/X        ; <---- Calculate N(i≠0)  -ln[(PMT*(p+1/i)+PV)/(PMT*(p+1/i)-FV)]/ln(1+i)
112 RCL+ "p"   ; Optimized the way this formula was calculated
113 RCL× "PMT"
114 ENTER
115 RCL+ "PV"
116 X<>Y
117 RCL- "FV"
118 ÷
119 LN
120 RCL "i"
121 LN1+X
122 ÷
123 +/-
124 RTN
125▸LBL B      ; <---- Calculate N(i=0)
126 RCL "PV"
127 RCL+ "FV"
128 +/-
129 RCL÷ "PMT" ; This is the calculated value of N
130 RTN
;---;--------------------------------------------------------------------------------------------
131▸LBL "PV"   ; Solve for PV (for i=0 & i≠0)
;---;--------------------------------------------------------------------------------------------
132 FUNC 01
133 SF 00      ; To signify this routine returns an answer in X
134 SF 01      ; Calculate "F" within XEQ I
135 XEQ I      ; creates "i" & "F" and leaves "i" in X 
136 X=0?
137 GTO C      ; <---- Calculate PV(i=0)   -(FV+PMT*N)
138 1/X        ; <---- Calculate PV(i≠0)   [PMT*(p+1/i)*(1-F)-FV]/F
139 RCL+ "p"   ; Optimized the way this formula was calculated
140 RCL× "PMT"
141 1
142 RCL- "F"
143 ×
144 RCL- "FV"
145 RCL÷ "F"
146 RTN
147▸LBL C      ; <---- Calculate PV(i=0)
148 RCL "PMT"
149 RCL× "N"
150 RCL+ "FV"
151 +/-
152 RTN
;---;--------------------------------------------------------------------------------------------
153▸LBL "FV"   ; Solve for FV (for i=0 & i≠0)
;---;--------------------------------------------------------------------------------------------
154 FUNC 01
155 SF 00      ; To signify this routine returns an answer in X
156 SF 01      ; Calculate "F" within XEQ I
157 XEQ I      ; creates "i" & "F" and leaves "i" in X 
158 X=0?
159 GTO D      ; <---- Calculate FV(i=0)   -(PV+PMT*N)
160 1/X        ; <---- Calculate FV(i≠0)   PMT*(p+1/i)*(1-F)-PV*F    merged XEQ 22
161 RCL+ "p"   ; Optimized the way this formula was calculated
162 RCL× "PMT"
163 1
164 RCL- "F"
165 ×
166 RCL "PV"
167 RCL× "F"
168 -
169 RTN
170▸LBL D      ; <---- Calculate FV(i=0)
171 RCL "PMT"
172 RCL× "N"
173 RCL+ "PV"
174 +/-
175 RTN
;---;--------------------------------------------------------------------------------------------
176▸LBL "PMT"  ; Solve for PMT (for i=0 & i≠0)
;---;--------------------------------------------------------------------------------------------
177 FUNC 01
178 SF 00      ; To signify this routine returns an answer in X
179 SF 01      ; Calculate "F" within XEQ I
180 XEQ I      ; creates "i" & "F" and leaves "i" in X 
181 X=0?
182 GTO E      ; <---- Calculate PMT(i=0)   (FV-PV)/N
183 1/X        ; <---- Calculate PMT(i≠0)   (PV*F+FV)/(p+1/i)/(1-F)   merged LBL 19
184 RCL+ "p"   ; Optimized the way this formula was calculated
185 1
186 RCL- "F"
187 ×
188 RCL "PV"
189 RCL× "F"
190 RCL+ "FV"
191 X<>Y
192 ÷
193 RTN
194▸LBL E      ; <---- Calculate PMT(i=0)
195 RCL "FV"
196 RCL- "PV"
197 RCL÷ "N"
198 RTN
;---;--------------------------------------------------------------------------------------------
199▸LBL "→AER" ;  Calculates 100*((1+i)^"P/YR"-1)
;---;--------------------------------------------------------------------------------------------
200 FUNC 01
201 SF 00      ; To signify this routine returns an answer in X
202 CF 01      ; Do NOT calculate "F" (which requires "N") within XEQ I
203 XEQ I      ; Went this route vs calculating "i" in-place
204 1
205 +
206 RCL "P/YR"
207 Y↑X
208 1
209 -
210 100
211 ×
212 RTN
;---;--------------------------------------------------------------------------------------------
213▸LBL I   ; Calculates i = "I%YR"/100/"P/YR" and (optionally) F = (1+i)^N
;   ;       ; (+2), Returns with "i" in X) Called by N, FV, PV, PMT, →AER
;---;--------------------------------------------------------------------------------------------
214 RCL "P/YR"
215 1/X
216 RCL "I%YR"
217 %
218 STO "i"    ; i = "I%YR"/100/"P/YR"
219 FC? 01     ; Need to catch if "N" is defined for the RCL× "N" below
220 RTN
221 LN1+X
222 RCL× "N"   ; ??? BUG if calculating N, FV, PV or PMT when "N" isn't defined yet! 1st time run 
223 E↑X
224 STO "F"    ; F = e^(N*ln(1+i))  <---- ??? isn't this just (1+i)^N
225 RCL "i"    ; ??? Changed this to return "i" vs "I%YR"
226 RTN
;---;--------------------------------------------------------------------------------------------
227▸LBL "I%YR" ;  (for Start_mode & End_mode) Consumes LBLs 24-5, 41-2, 30-7
;---; Holy cow this is unbelievably complex! Consumes 10 local labels.
;   ; This should also XEQ I
;---;--------------------------------------------------------------------------------------------
228 FUNC 01
229 LNSTK
230 SF 00      ; To signify this routine returns an answer in X
231 0=? "p"    ; (removed RCL "p", X=0?)
232 GTO 41     ; End mode
233 RCL "PV"   ; <---- Calculate I%YR(Start_mode)
234 RCL+ "PMT"
235 LSTO "pv"   ; pv = PV+PMT
236 RCL "FV"
237 RCL- "PMT"
238 LSTO "fv"   ; fv = FV-PMT
239 GTO 42
;---;-----------------------
240▸LBL 41     ; <---- Calculate I%YR(End_mode) - simply copy values
;---;-----------------------
241 RCL "FV"
242 LSTO "fv"
243 RCL "PV"
244 LSTO "pv"
;---;-----------------------
245▸LBL 42     ; referenced by 239
;---;-----------------------
246 0=? "IYR?" ; check if a guess has been input (removed RCL "IYR?", X=0?)
247 XEQ 24     ; if not, run interest rate guess
248 STO "I%YR" ; starting interest rate,  ??? another redundant calculation of "i"
249 RCL "P/YR"
250 1/X
251 %
252 STO "i"    ; starting i = "I%YR"/100/"P/YR"
253 CLX 
254 2
255 LSTO "g"    ; force at least 2 cycles of the loop
256 CLX
257 LSTO "f"
258 CLX        ; used to be DROP
;---;-----------------------
259▸LBL 30     ; start of Newton solve (adapted from plus42).
;              ; (+3) Spans LBLs 30-37. referenced by 380, 386, 400
;---;-----------------------
260 RCL "f"
261 LSTO "eps"
262 LSTO "f0"
263 CLX
264 RCL "i"
265 ENTER
266 ×          ; ??? why not just do x^2
267 1
268 +
269 1
270 X=Y?       ; check if i is very small by comparing 1 with 1+i^2
271 GTO 31     ; if so switch to Secant method (do people really care about this!?)
272 DROPN 2
273 RCL "i"
274 LN1+X
275 RCL× "N"
276 E↑X-1
277 RCL "i"
278 X<>Y
279 ÷
280 DUP
281 RCL "N"
282 ×
283 1
284 -
285 LSTO "y"
286 DROP
287 RCL "pv"
288 RCL+ "fv"
289 ×
290 LSTO "k"
291 RCL "pv"
292 RCL× "i"
293 +
294 RCL+ "PMT"
295 LSTO "f"
296 DROP
297 RCL "N"
298 1
299 -
300 RCL× "i"
301 RCL+ "y"
302 LSTO "num"
303 DROP
304 RCL "i"
305 ENTER
306 ×
307 RCL+ "i"
308 LSTO "den"  ; ??? only used 3 & 7 lines later (use the stack)
309 DROP
310 RCL "f"
311 RCL× "den"
312 RCL "k"
313 RCL× "num"
314 RCL "pv"
315 RCL× "den"
316 -
317 ÷
318 LSTO "eps"
319 RCL+ "i"
320 STO "i"
321 DROP
322 GTO 32     ; check success of iteration
;---;-----------------------
323▸LBL 31     ; The secant method. (+1) referenced by 271
;---;-----------------------
324 DROPN 2
325 RCL "pv"
326 RCL+ "fv"
327 RCL÷ "N"
328 LSTO "a"
329 DROP
330 RCL "pv"
331 RCL- "fv"
332 LSTO "b"
333 DROP
334 RCL "a"
335 RCL+ "b"
336 2
337 ÷
338 STO "fp"
339 DROP
340 RCL "N"
341 ENTER
342 ×
343 1
344 -
345 RCL× "a"
346 6
347 ÷
348 LSTO "fpp"   ; ??? only used once 8 lines later (use the stack)
349 DROP
350 RCL "fp"
351 RCL× "i"
352 RCL+ "PMT"
353 RCL+ "a"
354 LSTO "f"
355 DROP
356 RCL "fpp"
357 RCL× "i"
358 RCL+ "fp"
359 LSTO "fp2"   ; ??? only used once 4 lines later (use the stack)
360 DROP
361 RCL "f"
362 +/-
363 RCL÷ "fp2"
364 LSTO "eps"
365 RCL+ "i"
366 STO "i"
367 DROP
;---;-----------------------
368▸LBL 32     ; (+2) referenced by 322
;---;-----------------------
369 RCL "g"    ; forces at least two cycles of loop
370 X>0?
371 GTO 34
372 DROP       ; this used to be GTO 35 which was only called from here so I merged it
373 RCL "f0"
374 ABS
375 RCL "f"
376 ABS
377 X≥Y?       ; is guess worsening or same?
378 GTO 36
379 DROPN 2
380 GTO 30     ; return to start, going to 30 with nothing on the stack
;---;-----------------------
381▸LBL 34     ; Decrement "g" and GTO 30 (+1) referenced by 371  ??? there has to be a way to do DSE "g"
;---;-----------------------
382 1
383 -
384 LSTO "g"
385 DROP
386 GTO 30     ; going to 30 with nothing on the stack
;---;-----------------------
387▸LBL 36     ; referenced by 378
;---;-----------------------
388 DROPN 2
389 RCL "eps"
390 1000
391 ÷
392 RCL "i"
393 X<>Y
394 -
395 RCL "i"
396 X=Y?       ; break if good guess
397 GTO 37
398 DROPN 2    ; merged in LBL 38
399 RCL "i"
400 GTO 30     ; going to 30 with an item on the stack  ???? (as opposed to LBL 32,34)
;---;-----------------------
401▸LBL 37     ; referenced by 397
;---;-----------------------
402 DROPN 3
403 RCL "eps"
404 2
405 ÷
406 RCL "i"
407 X<>Y
408 -           ; last improvement to i solve post break.
409 RCL× "P/YR" ; i -> I%YR
410 100
411 ×
412 RTN
;---;-----------------------
413▸LBL 24     ; initial interest rate guess (via Albert Chan's code). Contains LBL 25. referenced by 247
;              ; Takes no inputs, returns a guess for I%YR
;---;-----------------------
414 RCL "fv"
415 RCL+ "pv"
416 RCL÷ "N"
417 LSTO "a"    ; a = (fv+pv)/N
418 RCL "fv"
419 RCL- "pv"
420 X<>Y
421 -
422 LSTO "b"    ; b = (fv-pv) - (fv+pv)/N
423 DROP
424 RCL "PMT"
425 RCL+ "a"
426 RCL÷ "b"
427 LSTO "c"    ; c = PMT+a+b   ??? only used once 9 lines later (use the stack)
428 RCL "N"
429 X↑2
430 RCL× "a"
431 ×
432 RCL÷ "b"
433 LSTO "d"    ; d = a/b*N^2   ??? only used once 4 lines later (use the stack)
434 3
435 -
436 RCL× "c"
437 RCL "d"
438 1.5
439 -
440 ÷
441 RCL× "P/YR" ; guess = "P/YR"*c*(d-3)/(d-1.5)     (one item added to the stack)
442 100
443 ×           ; guess = 100*"P/YR"*c*(d-3)/(d-1.5)     (one item added to the stack)
444 X>=0?       ; if guess is positive then return 
445 RTN
446 RCL "fv"    ; back up i guess (edge guess). referenced by 506 (was LBL 25)
447 RCL "pv"
448 ×           ; 2 items on the stack at this point
449 X≠0?        ; fv*pv = 0?
450 GTO 25
451 DROP 
452 RTN
453 LBL 25
454 DROPN 2     ; Y-register no longer needed.  
455 RCL "PMT"
456 RCL÷ "fv"
457 RCL "PMT"
458 +/-
459 RCL÷ "pv"
460 X>Y?        ; this can get cleaned up
461 X<>Y
462 CLX
463 100
464 ×
465 RTN
466 END
